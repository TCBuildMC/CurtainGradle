plugins {
    id "java-library"
    id "groovy"
    id "maven-publish"
}

group = project.maven_group
version = project.release_version

base {
    archivesName = project.archives_base_name
}

repositories {
    gradlePluginPortal()
    mavenCentral()
}

dependencies {
    annotationProcessor(compileOnly("org.projectlombok:lombok:1.18.32"))
    compileOnly gradleApi()
    compileOnly localGroovy()
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.2"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.2"
    implementation "commons-io:commons-io:2.16.1"

    testImplementation platform('org.junit:junit-bom:5.10.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testAnnotationProcessor(testCompileOnly("org.projectlombok:lombok:1.18.32"))
    testCompileOnly gradleApi()
    testCompileOnly localGroovy()
}

processResources {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

test {
    useJUnitPlatform()

    ignoreFailures true

    testLogging {
        exceptionFormat = "full"
        events "passed", "skipped", "failed"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach { tsk ->
    tsk.options.release = 8
    tsk.options.encoding = "UTF-8"
}

tasks.withType(GroovyCompile).configureEach { tsk ->
    tsk.options.encoding = "UTF-8"
}

tasks.withType(Test).configureEach { tsk ->
    tsk.systemProperty "file.encoding", "UTF-8"
}

jar {
    manifest {
        attributes "Implementation-Version": project.version
    }
}

compileJava.dependsOn clean

publishing {
    publications {
        create("MavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}
